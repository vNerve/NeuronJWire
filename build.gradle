plugins {
    id 'java'
    id 'maven-publish'
}

group 'dd.center'
version '1.2'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

sourceSets {
    generated {
        java {
            srcDir 'src/generated/java'
        }
    }
}

dependencies {
    implementation sourceSets.generated.output
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.12.4'
    generatedImplementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.12.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    dependsOn generatedClasses
    from sourceSets.generated.output
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/vnerve/NeuronJWire")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}